<h1>oj</h1>
<p>A unified templating framework for the people. Thirsty people.</p>
<h2>oj function</h2>
<p>Convert ojml to dom</p>
<pre><code>oj = module.exports = -&gt;
  # Prevent oj method from propagating
  _.argumentsPush()
  ojml = oj.emit.apply @, arguments
  _.argumentsPop()
  ojml</code></pre>
<p>Emit arguments as if it was an oj tag function</p>
<pre><code>oj.emit = -&gt;
  ojml = oj.tag &#39;oj&#39;, arguments...</code></pre>
<p>Keep a reference to ourselves for templates to see</p>
<pre><code>oj.oj = oj</code></pre>
<h2>oj.begin</h2>
<pre><code>oj.begin = (page) -&gt;

  # Defer dom manipulation until the page has loaded
  _readyOrLoad -&gt;

    # Compile only the body and below
    bodyOnly = html:1, doctype:1, head:1, link:1, script:1
    {dom,types} = oj.compile dom:1, html:0, css:0, ignore:bodyOnly, (require page)

    if not dom?
      console.error &#39;oj: dom failed to compile&#39;
      return

    # Find body
    body = document.getElementsByTagName(&#39;body&#39;)
    if body.length == 0
      console.error &#39;oj: &lt;body&gt; was not found&#39;
      return
    body = body[0]

    # Clear body and insert dom elements
    body.innerHTML = &#39;&#39;
    if not oj.isArray dom
      dom = [dom]
    for d in dom
      body.appendChild d

    for t in types
      t.inserted()

    # Trigger events bound through oj.ready
    oj.ready()</code></pre>
<h2>_readyOrLoad</h2>
<p>Helper method that loads with either ready or onload (whichever exists)</p>
<pre><code>_readyOrLoad = (fn) -&gt;</code></pre>
<p>  Use jquery ready if it exists</p>
<pre><code>  if jQuery?
    jQuery fn</code></pre>
<p>  Use onload</p>
<pre><code>  else
    # Add onload if it hasn&#39;t happened yet
    if document.readyState != &quot;complete&quot;
      prevOnLoad = window.onload
      window.onload = -&gt;
        prevOnLoad?()
        fn()</code></pre>
<p>  Call the function immediately if onload already happened</p>
<pre><code>    else
      fn()
  return</code></pre>
<h2>oj.ready</h2>
<pre><code>_readyQueue = queue:[], loaded:false
oj.ready = (f) -&gt;
  # Call everything if no arguments
  if oj.isUndefined f
    _readyQueue.loaded = true
    while (f = _readyQueue.queue.shift())
      f()
  # Call load if already loaded
  else if _readyQueue.loaded
    f()
  # Queue function for later
  else
    _readyQueue.queue.push f
  return</code></pre>
<h2>oj.id</h2>
<p>Generate a unique oj id</p>
<pre><code>oj.id = (len, chars) -&gt;
  &#39;oj&#39; + oj.guid len, chars</code></pre>
<h2>oj.guid</h2>
<pre><code>_randomInteger = (min, max) -&gt;
  return null if min == null or max == null or min &gt; max
  diff = max - min;
  # random int from zero to number minus one
  rnd = Math.floor Math.random() * (diff + 1)
  rnd + min

_chars = &quot;0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz&quot;.split &#39;&#39;
oj.guid = (len = 8, chars = _chars) -&gt;
  # Default arguments
  base = chars.length

  # Calculate how many chars can be determined by each random call
  charsPerRand = Math.floor Math.log(Math.pow(2,31)-1) / Math.log(base)
  randMin = 0
  randMax = Math.pow(base, charsPerRand)-1

  # Calculate random chars by calling random the minimum number of times
  output = &quot;&quot;
  for i in [0...len]
    # Generate random number
    if i % charsPerRand == 0
      rand = _randomInteger randMin, randMax
    charNext = chars[rand % base]
    output += charNext
    rand = Math.floor(rand / base)

  output</code></pre>
<h2>Register require.extension for .oj files in node</h2>
<pre><code>if require.extensions

  coffee = require &#39;coffee-script&#39;

  stripBOM = (c) -&gt; if c.charCodeAt(0) == 0xFEFF then (c.slice 1) else c

  wrapJS = (code) -&gt;
    &quot;(function(){with(oj.sandbox){#{code}}}).call(this);&quot;

  wrapCSMessage = (message, filepath) -&gt;
    &quot;#{oj.codes?.red}coffee-script error in #{filepath}: #{message}#{oj.codes?.reset}&quot;
  wrapJSMessage = (message, filepath) -&gt;
    &quot;#{oj.codes?.red}javascript error in #{filepath}: #{message}#{oj.codes?.reset}&quot;

  compileJS = (module, code, filepath) -&gt;
    code = wrapJS code
    global.oj = oj
    module._compile code, filepath
    delete global.oj

  # .oj files are compiled as javascript
  require.extensions[&#39;.oj&#39;] = (module, filepath) -&gt;
    code = stripBOM fs.readFileSync filepath, &#39;utf8&#39;
    try
      compileJS module, code, filepath
    catch eJS
      eJS.message = wrapJSMessage eJS.message, filepath
      throw eJS

  # .ojc files are compiled as coffee-script
  require.extensions[&#39;.ojc&#39;] = (module, filepath) -&gt;
    code = stripBOM fs.readFileSync filepath, &#39;utf8&#39;

    # Compile in coffee-script
    try
      code = coffee.compile code, bare: true
    catch eCoffee
      eCoffee.message = wrapCSMessage eCoffee.message, filepath
      throw eCoffee

    # Compile javascript
    try
      compileJS module, code, filepath

    catch eJS
      eJS.message = wrapJSMessage eJS.message, filepath
      throw eJS

  # TODO: .ojlc files are compiled as literate coffee-script</code></pre>
<h2>Export Model to NodeJS or globally</h2>
<p>Remember root context</p>
<pre><code>root = @</code></pre>
<p>Define oj version</p>
<pre><code>oj.version = &#39;0.0.14&#39;</code></pre>
<p>Detect if this is the client or server</p>
<pre><code>oj.isClient = not process?.versions?.node</code></pre>
<p>Export as a module in NodeJS</p>
<pre><code>if typeof module != &#39;undefined&#39;
  exports = module.exports = oj</code></pre>
<p>Export globally if not in NodeJS</p>
<pre><code>else
  root[&#39;oj&#39;] = oj</code></pre>
<h2>Type Helpers</h2>
<p>Based on <a href="http://underscorejs.org/">underscore.js</a>
The potential duplication saddens me but oj needs sophisticated type detection</p>
<p>Store short names for commonly used prototypes and methods</p>
<pre><code>ArrayP = Array.prototype
FuncP = Function.prototype
ObjP = Object.prototype

slice = ArrayP.slice
unshift = ArrayP.unshift
concat = ArrayP.concat</code></pre>
<p>Define</p>
<pre><code>oj.isUndefined = (obj) -&gt; obj == undefined
oj.isBoolean = (obj) -&gt; obj == true or obj == false or ObjP.toString.call(obj) == &#39;[object Boolean]&#39;
oj.isNumber = (obj) -&gt; !!(obj == 0 or (obj and obj.toExponential and obj.toFixed))
oj.isString = (obj) -&gt; !!(obj == &#39;&#39; or (obj and obj.charCodeAt and obj.substr))
oj.isDate = (obj) -&gt; !!(obj and obj.getTimezoneOffset and obj.setUTCFullYear)
oj.isFunction = (obj) -&gt; typeof obj == &#39;function&#39;
oj.isArray = Array.isArray or (obj) -&gt; ObjP.toString.call(obj) == &#39;[object Array]&#39;
oj.isRegEx = (obj) -&gt; ObjP.toString.call(obj) == &#39;[object RegExp]&#39;
oj.isDOM = (obj) -&gt; !!(obj and obj.nodeType?)
oj.isDOMElement = (obj) -&gt; !!(obj and obj.nodeType == 1)
oj.isDOMAttribute = (obj) -&gt; !!(obj and obj.nodeType == 2)
oj.isDOMText = (obj) -&gt; !!(obj and obj.nodeType == 3)
oj.isjQuery = (obj) -&gt; !!(obj and obj.jquery)
oj.isEvented = (obj) -&gt; !!(obj and obj.on and obj.off and obj.trigger)
oj.isOJ = (obj) -&gt; !!(obj?.isOJ)
oj.isArguments = (obj) -&gt; ObjP.toString.call(obj) == &#39;[object Arguments]&#39;
oj.isDefined = (obj) -&gt; not (typeof obj == &#39;undefined&#39;)

# typeOf: Mimic behavior of built-in typeof operator and integrate jQuery, Backbone, and OJ types
oj.typeOf = (any) -&gt;

  return &#39;null&#39; if any == null
  t = typeof any
  if t == &#39;object&#39;
    if oj.isArray any                     then t = &#39;array&#39;
    else if oj.isOJ any                   then t = any.typeName
    else if oj.isRegEx any                then t = &#39;regexp&#39;
    else if oj.isDate any                 then t = &#39;date&#39;
    else if oj.isDOMElement any           then t = &#39;dom-element&#39;
    else if oj.isDOMText any              then t = &#39;dom-text&#39;
    else if oj.isDOMAttribute any         then t = &#39;dom-attribute&#39;
    else if oj.isjQuery any               then t = &#39;jquery&#39;
    else                                  t = &#39;object&#39;
  t

oj.parse = (str) -&gt;
  if str == &#39;undefined&#39;
    undefined
  else if str == &#39;null&#39;
    null
  else if str == &#39;true&#39;
    true
  else if str == &#39;false&#39;
    false
  else if !(isNaN(number = parseFloat str))
    number
  else
    str

# Determine if obj is a vanilla object
oj.isObject = (obj) -&gt; (oj.typeOf obj) == &#39;object&#39;</code></pre>
<h2>Utility: Helpers</h2>
<pre><code>oj.__ = _ = {}
_.isCapitalLetter = (c) -&gt; !!(c.match /[A-Z]/)
_.identity = (v) -&gt; v
_.property = (obj, options = {}) -&gt;
  # _.defaults options, configurable: false
  Object.defineProperty obj, options
_.has = (obj, key) -&gt; ObjP.hasOwnProperty.call(obj, key)
_.keys = Object.keys || (obj) -&gt;
  throw &#39;Invalid object&#39; if obj != Object(obj)
  keys = [];
  for key of obj
    if _has obj, key
      keys[keys.length] = key;
  keys
_.values = (obj) -&gt;
  throw &#39;Invalid object&#39; if obj != Object(obj)
  out = []
  _.each obj, (v) -&gt; out.push v
  out

_.flatten = (array, shallow) -&gt;
  _.reduce array, ((memo, value) -&gt;
    if oj.isArray value
      return memo.concat(if shallow then value else _.flatten(value))
    memo[memo.length] = value
    memo
  ), []

_.reduce = (obj = [], iterator, memo, context) -&gt;
  initial = arguments.length &gt; 2
  if ArrayP.reduce and obj.reduce == ArrayP.reduce
    if context
      iterator = _.bind iterator, context
    return if initial then obj.reduce iterator, memo else obj.reduce iterator

  _.each obj, (value, index, list) -&gt;
    if (!initial)
      memo = value
      initial = true
    else
      memo = iterator.call context, memo, value, index, list

  if !initial
    throw new TypeError &#39;Reduce of empty array with no initial value&#39;
  memo

  ctor = -&gt;
  _.bind = (func, context) -&gt;
    if func.bind == FuncP.bind and FuncP.bind
      return FuncP.bind.apply func, slice.call(arguments, 1)
    throw new TypeError unless oj.isFunction(func)
    args = slice.call arguments, 2
    return bound = -&gt;
      unless this instanceof bound
        return func.apply context, args.concat(slice.call arguments)
      ctor.prototype = func.prototype
      self = new ctor
      result = func.apply self, args.concat(slice.call(arguments))
      if Object(result) == result
        return result
      self

_.sortedIndex = (array, obj, iterator = _.identity) -&gt;
  low = 0
  high = array.length;
  while low &lt; high
    mid = (low + high) &gt;&gt; 1;
    if iterator(array[mid]) &lt; iterator(obj) then low = mid + 1 else high = mid;
  low

_.indexOf = (array, item, isSorted) -&gt;
  return -1 unless array?
  if isSorted
    i = _.sortedIndex array, item
    return if array[i] == item then i else -1
  if (ArrayP.indexOf &amp;&amp; array.indexOf == ArrayP.indexOf)
    return array.indexOf item
  for v, i in array
    if v == item
      return i
  -1

_.toArray = (obj) -&gt;
  return [] if !obj
  return slice.call obj if oj.isArray obj
  return slice.call obj if oj.isArguments obj
  return obj.toArray() if obj.toArray and oj.isFunction(obj.toArray)
  _.values obj

# Determine if object or array is empty
_.isEmpty = (obj) -&gt;
  return obj.length == 0 if oj.isArray obj
  for k of obj
    if _.has obj, k
      return false
  true

_.clone = (obj) -&gt;
  # TODO: support cloning OJ instances
  # TODO: support options, deep: true
  return obj unless oj.isObject obj
  if oj.isArray obj then obj.slice() else _.extend {}, obj

_.contains = (obj, target) -&gt;
  if not obj?
    return false
  if ArrayP.indexOf and obj.indexOf == ArrayP.indexOf
    return obj.indexOf(target) != -1
  _.any obj, (value) -&gt; value == target

_.some = _.any = (obj, iterator, context) -&gt;
    iterator ?= _.identity
    result = false
    if not obj?
      return result
    if ArrayP.some and obj.some == ArrayP.some
      return obj.some iterator, context
    _.each obj, (value, index, list) -&gt;
      if result or (result = iterator.call(context, value, index, list))
        return breaker
    return !!result</code></pre>
<h2>Utility: Iteration</h2>
<pre><code>_.breaker = {}

_.each = (col, iterator, context) -&gt;

  return if col == null
  if ArrayP.forEach and col.forEach == ArrayP.forEach
    col.forEach iterator, context
  else if oj.isArray col
    for v, i in col
      if iterator.call(context, v, i, col) == _.breaker
        return _.breaker
  else
    for k, v of col
      if _.has col, k
        if iterator.call(context, v, k, col) == _.breaker
          return _.breaker

_.map = (obj, iterator, options = {}) -&gt;

  context = options.context
  recurse = options.recurse
  evaluate = options.evaluate

  # Recurse if necessary
  iterator_ = iterator
  if recurse
    do (options) -&gt;
      iterator_ = (v,k,o) -&gt;
        options_ = _.extend (_.clone options), (key: k, object: v)
        _.map v, iterator, options_

  # Evaluate functions if necessary
  if oj.isFunction obj

    # Functions pass through if evaluate isn&#39;t set
    return obj unless evaluate

    while evaluate and oj.isFunction obj
      obj = obj()

  out = obj

  # Array case
  if oj.isArray obj
    out = []
    return out unless obj
    return (obj.map iterator_, context) if ArrayP.map and obj.map == ArrayP.map
    _.each(obj, ((v, ix, list) -&gt;
      out[out.length] = iterator_.call context, v, ix, list
    ))

    if obj.length == +obj.length
      out.length = obj.length

  # Object case
  else if oj.isObject obj
    out = {}
    return out unless obj
    for k,v of obj
      # Returning undefined will omit the thing
      if (r = iterator_.call(context, v, k, obj)) != undefined
        out[k] = r
  # Basis of recursive case
  else
    return iterator.call context, obj, options.key, options.object,
  out</code></pre>
<p>  extend</p>
<pre><code>_.extend = (obj) -&gt;
  _.each(slice.call(arguments, 1), ((source) -&gt;
    for key, value of source
      obj[key] = value
  ))
  obj</code></pre>
<p>  defaults</p>
<pre><code>_.defaults = (obj) -&gt;
  _.each(slice.call(arguments, 1), ((source) -&gt;
    for prop of source
      if not obj[prop]?
        obj[prop] = source[prop]
  ))
  obj</code></pre>
<p>  omit</p>
<pre><code>_.omit = (obj) -&gt;
  copy = {}
  keys = concat.apply ArrayP, slice.call(arguments, 1)
  for key of obj
    if not _.contains keys, key
      copy[key] = obj[key]
  copy</code></pre>
<p>  uniqueSort</p>
<pre><code>_.uniqueSort = (array, isSorted = false) -&gt;
  if not isSorted
    array.sort()
  out = []
  for item,ix in array
    if ix &gt; 0 and array[ix-1] == array[ix]
      continue
    out.push item
  out

_.uniqueSortedUnion = (array, array2) -&gt;
  _.uniqueSort (array.concat array2)

_.debounce = (wait, func, immediate) -&gt;
  timeout = null
  result = null
  -&gt;
    context = @
    args = arguments
    later = -&gt;
      timeout = null;
      if !immediate
        result = func.apply context, args

    callNow = immediate and !timeout
    clearTimeout timeout
    timeout = setTimeout later, wait
    if callNow
      result = func.apply context, args
    result</code></pre>
<h2>Path Helpers</h2>
<p>Based on node.js/path module
All we need is join,resolve,dirname</p>
<pre><code>pathNormalizeArray = (parts, allowAboveRoot) -&gt;
  up = 0
  i = parts.length - 1
  while i &gt;= 0
    last = parts[i]
    if last == &#39;.&#39;
      parts.splice i, 1
    else if last == &#39;..&#39;
      parts.splice i, 1
      up++
    else if up
      parts.splice i, 1
      up--
    i--

  if allowAboveRoot
    while up--
      parts.unshift &#39;..&#39;

  parts

pathSplitRe = /^(\/?)([\s\S]+\/(?!$)|\/)?((?:\.{1,2}$|[\s\S]+?)?(\.[^.\/]*)?)$/
pathSplit = (filename) -&gt;
  result = pathSplitRe.exec filename
  [result[1] or &#39;&#39;, result[2] or &#39;&#39;, result[3] or &#39;&#39;, result[4] or &#39;&#39;]

_.pathResolve = -&gt;
  resolvedPath = &#39;&#39;
  resolvedAbsolute = false
  i = arguments.length-1
  while i &gt;= -1 and !resolvedAbsolute
    path = if (i &gt;= 0) then arguments[i] else process.cwd()
    if (typeof path != &#39;string&#39;) or !path
      continue
    resolvedPath = path + &#39;/&#39; + resolvedPath
    resolvedAbsolute = path.charAt(0) == &#39;/&#39;
    i--
  resolvedPath = pathNormalizeArray(resolvedPath.split(&#39;/&#39;).filter((p) -&gt;
    return !!p
  ), !resolvedAbsolute).join(&#39;/&#39;)

  ((if resolvedAbsolute then &#39;/&#39; else &#39;&#39;) + resolvedPath) or &#39;.&#39;

_.pathNormalize = (path) -&gt;
  isAbsolute = path.charAt(0) == &#39;/&#39;
  trailingSlash = path.substr(-1) == &#39;/&#39;

  # Normalize the path
  path = pathNormalizeArray(path.split(&#39;/&#39;).filter((p) -&gt;
    !!p
  ), !isAbsolute).join(&#39;/&#39;)

  if !path and !isAbsolute
    path = &#39;.&#39;

  if path and trailingSlash
    path += &#39;/&#39;

  (if isAbsolute then &#39;/&#39; else &#39;&#39;) + path

_.pathJoin = -&gt;
  paths = Array.prototype.slice.call arguments, 0
  _.pathNormalize(paths.filter((p, index) -&gt;
    p and typeof p == &#39;string&#39;
  ).join(&#39;/&#39;))

_.pathDirname = (path) -&gt;
  result = pathSplit path
  root = result[0]
  dir = result[1]
  if !root and !dir
    # No dirname whatsoever
    return &#39;.&#39;
  if dir
    # It has a dirname, strip trailing slash
    dir = dir.substr 0, dir.length - 1
  root + dir</code></pre>
<h2>oj.dependency</h2>
<p>Ensure dependencies through oj plugins</p>
<pre><code>oj.dependency = (name) -&gt;
  obj = if oj.isClient then window[name] else global[name]
  throw new Error(&quot;oj: #{name} dependency is missing&quot;) unless oj.isDefined obj</code></pre>
<p>Depend on jQuery</p>
<pre><code>oj.dependency &#39;jQuery&#39;</code></pre>
<h2>oj.addMethod</h2>
<p>Add multiple methods to an object</p>
<pre><code>oj.addMethods = (obj, mapNameToMethod) -&gt;
  for methodName, method of mapNameToMethod
    oj.addMethod obj, methodName, method
  return</code></pre>
<h2>oj.addMethod</h2>
<p>Add method to an object</p>
<pre><code>oj.addMethod = (obj, methodName, method) -&gt;</code></pre>
<p>  Validate input
      throw &#39;oj.addMethod: object expected for first argument&#39; unless oj.isObject obj
      throw &#39;oj.addMethod: string expected for second argument&#39; unless oj.isString methodName
      throw &#39;oj.addMethod: function expected for thrid argument&#39; unless oj.isFunction method</p>
<p>  Methods are non-enumerable, non-writable properties</p>
<pre><code>  Object.defineProperty obj, methodName,
    value: method
    enumerable: false
    writable: false
    configurable: true
  return</code></pre>
<h2>oj.removeMethod</h2>
<p>Remove a method from an object</p>
<pre><code>oj.removeMethod = (obj, methodName) -&gt;</code></pre>
<p>  Validate inputs
      throw &#39;oj.removeMethod: string expected for second argument&#39; unless oj.isString methodName</p>
<p>  Remove the method
      delete obj[methodName]
      return</p>
<h2>oj.addProperties</h2>
<p>Add multiple properties to an object</p>
<p>Properties can be specified by get/set methods or by a value</p>
<p>Value Property:
<code>age: 7    # defaults to {writable:true, enumerable:true}</code></p>
<p>Value Property with specified writable/enumerable:
<code>age: {value:7, writable:false, enumerable:false}</code></p>
<p>Readonly Get Property
<code>age: {get:(-&gt; 7)}</code></p>
<pre><code>oj.addProperties = (obj, mapNameToInfo) -&gt;</code></pre>
<p>  Iterate over properties</p>
<pre><code>  for propName, propInfo of mapNameToInfo</code></pre>
<p>  Wrap the value if propInfo is not already a property definition</p>
<pre><code>    if not propInfo?.get? and not propInfo?.value?
      propInfo = value: propInfo, writable:true

    oj.addProperty obj, propName, propInfo

  return</code></pre>
<h2>oj.addProperty</h2>
<pre><code>oj.addProperty = (obj, propName, propInfo) -&gt;</code></pre>
<p>  Validate input</p>
<pre><code>  # throw new Error(&#39;oj.addProperty: obj expected for first argument&#39;) unless oj.isObject obj

  throw new Error(&#39;oj.addProperty: string expected for second argument&#39;) unless oj.isString propName

  throw new Error(&#39;oj.addProperty: object expected for third argument&#39;) unless oj.isObject propInfo</code></pre>
<p>  Default properties to enumerable and configurable</p>
<pre><code>  _.defaults propInfo,
    enumerable: true
    configurable: true</code></pre>
<p>  Remove property if it already exists</p>
<pre><code>  if Object.getOwnPropertyDescriptor(obj, propName)?
    oj.removeProperty obj, propName</code></pre>
<p>  Add the property</p>
<pre><code>  Object.defineProperty obj, propName, propInfo
  return</code></pre>
<h2>oj.removeProperty</h2>
<pre><code>oj.removeProperty = (obj, propName) -&gt;</code></pre>
<p>  Validate input</p>
<pre><code>  throw &#39;oj.addProperty: string expected for second argument&#39; unless oj.isString propName</code></pre>
<p>  Remove property</p>
<pre><code>  delete obj[propName]</code></pre>
<h2>oj.isProperty</h2>
<pre><code># Determine if the specified key is was defined by addProperty

oj.isProperty = (obj, propName) -&gt;
  throw &#39;oj.isProperty: string expected for second argument&#39; unless oj.isString propName

  Object.getOwnPropertyDescriptor(obj, propName).get?</code></pre>
<h2>oj.copyProperty</h2>
<p>Determine copy source.propName to dest.propName</p>
<pre><code>oj.copyProperty = (dest, source, propName) -&gt;
  info = Object.getOwnPropertyDescriptor source, propName
  Object.defineProperty dest, propName, info</code></pre>
<h2>_.arguments</h2>
<p>Abstraction to wrap global arguments stack. This makes me sad but it is necessary for div -&gt; syntax</p>
<p>  Stack of results</p>
<pre><code>_.argumentsStack = []</code></pre>
<p>  Result is top of the stack</p>
<pre><code>oj.addProperty _, &#39;arguments&#39;, get: -&gt; if _.argumentsStack.length then _.argumentsStack[_.argumentsStack.length-1] else null</code></pre>
<p>  Push scope onto arguments</p>
<pre><code>_.argumentsPush = (args = []) -&gt;
  _.argumentsStack.push args
  return</code></pre>
<p>  Pop scope from arguments</p>
<pre><code>_.argumentsPop = -&gt;
  if _.argumentsStack.length
    return _.argumentsStack.pop()
  null</code></pre>
<p>  Append argument</p>
<pre><code>_.argumentsAppend = (arg) -&gt;
  if _.arguments
    _.arguments.push arg
  return

# oj.tag (name, attributes, content, content, ...)
# ------------------------------------------------------------------------------
#     name          String of the tag to serialize
#     attributes    (Optional) Object defining attributes of tag being serialized
#                   Keys have smart mappings:
#                       &#39;c&#39;  will map to &#39;class&#39;
#                       &#39;fontSize&#39; will map to &#39;font-size&#39;
#                       &#39;borderRadius&#39; will map to &#39;moz-border-radius&#39;, etc.

oj.tag = (name, args...) -&gt;

  throw &#39;oj.tag error: argument 1 is not a string (expected tag name)&#39; unless oj.isString name

  # Build ojml starting with tag
  ojml = [name]

  # Get attributes from args by unioning all objects
  attributes = {}
  for arg in args
    # TODO: evaluate argument if necessary
    if oj.isObject arg
      _.extend attributes, arg

  # Help the attributes out as they have shitting
  attributes = _tagAttributes name, attributes

  # Add attributes to ojml if they exist
  ojml.push attributes unless _.isEmpty attributes

  # Push arguments to build up children tags
  _.argumentsPush ojml

  # Loop over attributes
  for arg in args
    if oj.isObject arg
      continue
    else if oj.isFunction arg

      len = _.arguments.length

      # Call the argument it will auto append to _.arguments which is ojml
      r = arg()

      # Use return value if _.arguments weren&#39;t changed
      if len == _.arguments.length and r?
        _.argumentsAppend r

    else
      _.argumentsAppend arg

  # Pop to restore previous context
  _.argumentsPop()

  # Append the final result to your parent&#39;s arguments
  # if there exists an argument to append to
  _.argumentsAppend ojml

  ojml

oj.tag.elements =
  closed: &#39;a abbr acronym address applet article aside audio b bdo big blockquote body button canvas caption center cite code colgroup command datalist dd del details dfn dir div dl dt em embed fieldset figcaption figure font footer form frameset h1 h2 h3 h4 h5 h6 head header hgroup html i iframe ins keygen kbd label legend li map mark menu meter nav noframes noscript object ol optgroup option output p pre progress q rp rt ruby s samp script section select small source span strike strong style sub summary sup table tbody td textarea tfoot th thead time title tr tt u ul var video wbr xmp&#39;.split &#39; &#39;
  open: &#39;area base br col command css !DOCTYPE embed hr img input keygen link meta param source track wbr&#39;.split &#39; &#39;

oj.tag.elements.all = (oj.tag.elements.closed.concat oj.tag.elements.open).sort()

oj.tag.isClosed = (tag) -&gt;
  (_.indexOf oj.tag.elements.open, tag, true) == -1

# Helper to set tag name on a tag
_setTagName = (tag, name) -&gt;
  if tag?
    tag.tagName = name
  return

# Helper to get tag name on a tag
_getTagName = (tag) -&gt;
  tag.tagName

# Helper to get instance on element
_getInstanceOnElement = (el) -&gt;
  if el?.oj?
    el.oj
  else
    null

# Helper to set instance on element
_setInstanceOnElement = (el, inst) -&gt;
  el?.oj = inst
  return

# Create tag methods
for t in oj.tag.elements.all
  do (t) -&gt;
    oj[t] = -&gt; oj.tag t, arguments...
    # Remember the tag name
    _setTagName oj[t], t

# Clear attributes
_defaultClear = (dest, d, e) -&gt;
  _.defaults dest, d
  for k of e
    delete dest[k]
  dest

# Adjust tag attributes
_tagAttributes = (name, attributes) -&gt;
  attr = _.clone attributes
  switch name

    # link tags default to stylesheet, text/css, url instead of href
    when &#39;link&#39; then _defaultClear attr, {rel:&#39;stylesheet&#39;, type:&#39;text/css&#39;, href: attr.url or attr.src}, {url:0, src:0}

    # script tags default to javascript, url instead of src
    when &#39;script&#39; then _defaultClear attr, {type:&#39;text/javascript&#39;, src: attr.url}, url:0

    # anchor tags can use url instead of href
    when &#39;a&#39; then _defaultClear attr, {href:attr.url}, url:0
  attr


# doctype tag
# ------------------------------------------------------------------------------

dhp = &#39;&lt;!DOCTYPE HTML PUBLIC &quot;-//W3C//DTD HTML 4.01&#39;
w3 = &#39;&quot;http://www.w3.org/TR/html4/&#39;
strict5 = &#39;&lt;!DOCTYPE html&gt;&#39;
strict4 = dhp+&#39;//EN&quot; &#39;+w3+&#39;strict.dtd&quot;&gt;&#39;

doctypes =
  &#39;5&#39;: strict5
  &#39;HTML 5&#39;: strict5
  &#39;4&#39;: strict4
  &#39;HTML 4.01 Strict&#39;: strict4
  &#39;HTML 4.01 Frameset&#39;: dhp+&#39; Frameset//EN&quot; &#39;+w3+&#39;frameset.dtd&quot;&gt;&#39;
  &#39;HTML 4.01 Transitional&#39;: dhp+&#39; Transitional//EN&quot; &#39;+w3+&#39;loose.dtd&quot;&gt;&#39;

oj.doctype = (type = 5) -&gt;
  # Emit like a tag
  ojml = doctypes[type]
  _.argumentsAppend ojml
  ojml

# oj.page
# ------------------------------------------------------------------------------
# General template for oj to not need html, head, body tags

oj.page = (options, content) -&gt;
  # Options is optional
  if not content?
    content = options
    options = {}

  oj.html -&gt;
    oj.head -&gt;
      if options.title?
        oj.title options.title
    oj.body -&gt;
      content()

# oj.styles = ()

# oj.extend (context)
# ------------------------------------------------------------------------------
#     Extend oj methods into a context. Common contexts are `global` `window`
#     and `this` (used in coffee script). Helper methods are not extended (oj._)
#
#     context          Object to extend oj methods and objects into

oj.extend = (context) -&gt;
  o = {}
  for k,v of oj
    if k[0] != &#39;_&#39;
      o[k] = v
  delete o.extend
  _.extend context, o

# oj.compile
# ------------------------------------------------------------------------------
# Compile ojml into meaningful parts
# options
#     html:true           Compile to html
#     dom:true            Compile to dom
#     css:true            Compile to css
#     debug:true          Keep all source including comments
#     ignore:{html:1}     Map of tags to ignore while compiling

oj.compile = (options, ojml) -&gt;

  # Options is optional
  if not ojml?
    ojml = options
    options = {}

  # Default options to compile everything
  options = _.defaults {}, options,
    html: true
    dom: true
    css: true
    cssMap: false
    debug: false
    ignore: {}

  # Always ignore oj and css tags
  _.extend options.ignore, oj:1, css:1

  acc = _.clone options
  acc.html = if options.html then [] else null    # html accumulator
  acc.dom = if options.dom and document? then (document.createElement &#39;OJ&#39;) else null
  acc.css = if options.css || options.cssMap then {} else null
  acc.indent = &#39;&#39;                                 # indent counter
  acc.types = [] if options.dom                   # remember types if making dom
  acc.tags = {}                                   # remember what tags were used
  _compileAny ojml, acc

  # Generate cssMap
  if options.cssMap
    cssMap = acc.css

  # Generate css
  if options.css
    css = _cssFromObject acc.css, options.debug

  # Generate HTML
  if options.html
    html = acc.html.join &#39;&#39;

  # Generate dom
  if options.dom

    # Remove the &lt;oj&gt; wrapping from the dom element
    dom = acc.dom.childNodes

    # Cleanup inconsistencies of childNodes
    if dom.length?
      # Make dom a real array
      dom = _.toArray dom
      # Filter out anything that isn&#39;t a dom element
      dom = dom.filter (v) -&gt; oj.isDOM(v)

    # Ensure dom is null if empty
    if dom.length == 0
      dom = null

    # Single elements are returned themselves not as a list
    # Reasoning: The common cases don&#39;t have multiple elements &lt;html&gt;,&lt;body&gt;
    # or the complexity doesn&#39;t matter because insertion is abstracted for you
    # In short it is easier to check for _.isArray dom, then _isArray dom &amp;&amp; dom.length &gt; 0
    else if dom.length == 1
      dom = dom[0]

  out = html:html, dom:dom, css:css, cssMap:cssMap, types:acc.types, tags:acc.tags

  out

_styleKeyFromFancy = (key) -&gt;
  out = &quot;&quot;
  # Loop over characters in key looking for camal case
  for c in key
    if _.isCapitalLetter c
      out += &quot;-#{c.toLowerCase()}&quot;
    else
      out += c
  out

# _styleFromObject: Convert object to string form
# -----------------------------------------------------------------------------
#
#     inline:false      inline:true                inline:false,indent:true
#     color:red;        color:red;font-size:10px   \tcolor:red;
#     font-size:10px;                              \tfont-size:10px;
#

_styleFromObject = (obj, options = {}) -&gt;
  _.defaults options,
    inline: true
    indent: false
  # Trailing semi should only exist on when we aren&#39;t indenting
  options.semi = !options.inline
  out = &quot;&quot;
  keys = _.keys(obj).sort()
  indent = if options.indent then &#39;\t&#39; else &#39;&#39;
  newline = if options.inline then &#39;&#39; else &#39;\n&#39;
  for kFancy,ix in keys
    # Add semi if it is not inline or it is not the last key
    semi = if options.semi or ix != keys.length-1 then &quot;;&quot; else &#39;&#39;
    k = _styleKeyFromFancy kFancy
    out += &quot;#{indent}#{k}:#{obj[kFancy]}#{semi}#{newline}&quot;
  out

# _attributesFromObject: Convert object to attribute string
# -----------------------------------------------------------------------------
# This object has nothing special. No renamed keys, no jquery events. It is
# precicely what must be serialized with no adjustment.
_attributesFromObject = (obj) -&gt;
  # Pass through non objects
  return obj if not oj.isObject obj

  out = &#39;&#39;
  # Serialize attributes in order for consistent output
  space = &#39;&#39;
  for k in _.keys(obj).sort()
    v = obj[k]

    # Boolean attributes have no value
    if (v == true)
      out += &quot;#{space}#{k}&quot;

    # Other attributes have a value
    else
      out += &quot;#{space}#{k}=\&quot;#{v}\&quot;&quot;

    space = &#39; &#39;
  out

# _cssFromObject:
# -----------------------------------------------------------------------------
# Convert css selectors and rules to a string
#
#     debug:true               debug:false
#     .cls {                   .cls{color: red}
#         color: red;
#     }\n

_cssFromObject = (cssMap, isDebug = 0) -&gt;
  newline = if isDebug then &#39;\n&#39; else &#39;&#39;
  space = if isDebug then &#39; &#39; else &#39;&#39;
  inline = !isDebug
  indent = isDebug
  css = &#39;&#39;
  for selector, styles of cssMap
    rules = _styleFromObject styles, inline:inline, indent:indent
    css += &quot;#{selector}#{space}{#{newline}#{rules}}#{newline}&quot;
  css

# Recursive helper for compiling that wraps indention
_compileDeeper = (method, ojml, options) -&gt;
  i = options.indent
  options.indent += &#39;\t&#39;
  method ojml, options
  options.indent = i

# Compile ojml or any type
pass = -&gt;
_compileAny = (ojml, options) -&gt;

  switch oj.typeOf ojml

    when &#39;array&#39;
      _compileTag ojml, options

    when &#39;jquery&#39;
      # TODO: Missing unit tests for the jquery case
      options.html?.push ojml.html()
      options.dom?.concat ojml.get()

    when &#39;string&#39;
      options.html?.push ojml
      if ojml.length &gt; 0 and ojml[0] == &#39;&lt;&#39;
        root = document.createElement &#39;div&#39;
        root.innerHTML = ojml
        els = root.childNodes
        options.dom?.appendChild root
        # for el in els
        #   options.dom?.appendChild el
      else
        options.dom?.appendChild document.createTextNode ojml

    when &#39;boolean&#39;, &#39;number&#39;
      options.html?.push &quot;#{ojml}&quot;
      options.dom?.appendChild document.createTextNode &quot;#{ojml}&quot;

    when &#39;function&#39;
      # Wrap function call to allow full oj generation within ojml
      _compileAny (oj ojml), options

    when &#39;date&#39;
      options.html?.push &quot;#{ojml.toLocaleString()}&quot;
      options.dom?.appendChild document.createTextNode &quot;#{ojml.toLocaleString()}&quot;

    # Do nothing for &#39;null&#39;, &#39;undefined&#39;, &#39;object&#39;
    when &#39;null&#39; then break
    when &#39;undefined&#39; then break
    when &#39;object&#39; then break

    else
      # OJ type
      if oj.isOJ ojml
        options.types?.push ojml
        options.html?.push ojml.toHTML()
        options.dom?.appendChild ojml.toDOM()
        if options.css?
          _.extend options.css, ojml.toCSSMap()

  return

# Supported events from jquery
jqueryEvents = bind:1, on:1, off:1, live:1, blur:1, change:1, click:1, dblclick:1, focus:1, focusin:1, focusout:1, hover:1, keydown:1, keypress:1, keyup:1, mousedown:1, mouseenter:1, mousemove:1, mouseout:1, mouseup:1, ready:1, resize:1, scroll:1, select:1

# Compile ojml tag (an array)
_compileTag = (ojml, options) -&gt;

  # Empty list compiles to nothing
  return if ojml.length == 0

  # Get tag
  tag = ojml[0]
  tagType = typeof tag

  # Allow ojml&#39;s tag parameter to be &#39;table&#39; or table or Table
  tag = if (tagType == &#39;function&#39; or tagType == &#39;object&#39;) and _getTagName(tag)? then _getTagName(tag) else tag

  # Fail if we couldn&#39;t find a string by now
  throw new Error(&#39;oj.compile: tag name is missing&#39;) unless oj.isString(tag) and tag.length &gt; 0

  # Create oj object if tag is capitalized
  if _.isCapitalLetter tag[0]
    return _compileDeeper _compileAny, (new oj[tag] ojml.slice(1)), options

  # Record tag
  options.tags[tag] = true

  # Get attributes (optional)
  attributes = null
  if oj.isObject ojml[1]
    attributes = ojml[1]

  # Remaining arguments are children of this tag
  children = if attributes then ojml.slice 2 else ojml.slice 1

  # Compile to css if requested
  if options.css and tag == &#39;css&#39;

    # Extend options.css with rules
    for selector,styles of attributes
      options.css[selector] ?= styles
      _.extend options.css[selector], styles

  # Compile to html if requested
  if not options.ignore[tag]

    events = _attributesProcessedForOJ attributes

    # Add dom element with attributes
    if options.dom and document?
      # Create element
      el = document.createElement tag

      # Add self to parent
      if oj.isDOMElement options.dom
        options.dom.appendChild el

      # Push ourselves on the dom stack (to handle children)
      options.dom = el

      # Set attributes in sorted order for consistency
      if oj.isObject attributes
        for attrName in _.keys(attributes).sort()
          attrValue = attributes[attrName]
          # Boolean attributes have no value
          if attrValue == true
            att = document.createAttribute attrName
            el.setAttributeNode att
          else
            el.setAttribute attrName, attrValue

      # Bind events
      _attributesBindEventsToDOM events, el

    # Add tag with attributes
    if options.html
      attr = (_attributesFromObject attributes) ? &#39;&#39;
      space = if attr == &#39;&#39; then &#39;&#39; else &#39; &#39;
      options.html.push &quot;&lt;#{tag}#{space}#{attr}&gt;&quot;


  # Compile your children if necessary
  for child in children
    # Skip intention if there is only one child
    if options.debug &amp;&amp; children.length &gt; 1
      options.html?.push &quot;\n\t#{options.indent}&quot;
    _compileDeeper _compileAny, child, options

  # Skip intention if there is only one child
  if options.debug &amp;&amp; children.length &gt; 1
    options.html?.push &quot;\n#{options.indent}&quot;

  # End html tag if you have children or your tag closes
  if not options.ignore[tag]
    # Close tag if html
    if options.html and (children.length &gt; 0 or oj.tag.isClosed(tag))
      options.html?.push &quot;&lt;/#{tag}&gt;&quot;
    # Pop ourselves if dom
    if options.dom
      options.dom = options.dom.parentNode

  return

# Allow attributes to take style as an object
_attributeStyleAllowsObject = (attr) -&gt;
  if oj.isObject attr?.style
    attr.style = _styleFromObject attr.style, inline:true
  return

# Allow attributes to alias c to class
_attributeCMeansClass = (attr) -&gt;
  if attr?.c?
    attr.class = attr.c
    delete attr.c
  return

# Allow attributes to take class as an array of strings
_attributeClassAllowsArrays = (attr) -&gt;
  if oj.isArray attr?.class
    attr.class = attr.join &#39; &#39;
  return

# Omit falsy values except for zero
_attributeOmitFalsyValues = (attr) -&gt;
  if oj.isObject attr
    # Filter out falsy except for 0
    for k,v of attr
      delete attr[k] if v == null or v == undefined or v == false

# Filter out jquery events
_attributesFilterOutEvents = (attr) -&gt;
  out = {}
  if oj.isObject attr
    # Filter out attributes that are jqueryEvents
    for k,v of attr
      # If this attribute (k) is an event
      if jqueryEvents[k]?
        out[k] = v
        delete attr[k]
  out

# All the OJ magic for attributes
_attributesProcessedForOJ = (attr) -&gt;

  # Alias c to class
  _attributeCMeansClass attr

  # style takes objects
  _attributeStyleAllowsObject attr

  # class takes arrays
  _attributeClassAllowsArrays attr

  # Omit keys that false, null, or undefined
  _attributeOmitFalsyValues attr

  # TODO: Consider jsoning anything that isn&#39;t a string
  # any keys that aren&#39;t strings are jsoned
  # _attributesJSONAllKeys attributes

  # Filter out jquery events
  events = _attributesFilterOutEvents attr

  # Returns bindable events
  events

# Bind events to dom
_attributesBindEventsToDOM = (events, el) -&gt;
  for ek, ev of events
    if $?
      if oj.isArray ev
        $(el)[ek].apply @, ev
      else
        $(el)[ek](ev)
    else
      console.error &quot;oj: jquery is missing when binding a &#39;#{ek}&#39; event&quot;

# oj.toHTML
# ------------------------------------------------------------------------------
# Make oj directly to HTML. It will ignore all event bindings

oj.toHTML = (options, ojml) -&gt;
  # Options is optional
  if not oj.isObject options
    ojml = options
    options = {}

  # Create html only
  _.extend options,
    dom: false
    js: false
    html: true
    css: false

  (oj.compile options, ojml).html

# oj.toCSS
# ------------------------------------------------------------------------------
# Make oj directly to css. It will ignore all event bindings and html

oj.toCSS = (options, ojml) -&gt;
  # Options is optional
  if not oj.isObject options
    ojml = options
    options = {}

  # Create html only
  _.extend options,
    dom: false
    js: false
    html: false
    css: true

  (oj.compile options, ojml).css

# _.inherit
# ------------------------------------------------------------------------------
# Based on, but sadly incompatable with, coffeescript inheritance
_.inherit = (child, parent) -&gt;

  # Copy class properties and methods
  for key of parent
    oj.copyProperty child, parent, key

  ctor = -&gt;
  ctor:: = parent::
  child:: = new ctor()

  # Provide easy access for base class methods
  # Example: Parent.base.methodName(arguments...)
  child.base = parent::

  return

# oj.argumentShift
# ------------------------------------------------------------------------------
# Helper to make argument handling easier

oj.argumentShift = (args, key) -&gt;
  if (oj.isObject args) and key? and args[key]?
    value = args[key]
    delete args[key]
  value

# oj.type
# ------------------------------------------------------------------------------

oj.type = (name, args = {}) -&gt;
  throw &#39;oj.type: string expected for first argument&#39; unless oj.isString name
  throw &#39;oj.type: object expected for second argument&#39; unless oj.isObject args

  args.methods ?= {}
  args.properties ?= {}

  # When auto newing you need to delay construct the properties
  # or they will be constructed twice.
  delay = &#39;__DELAYED__&#39;
  Out = new Function(&quot;&quot;&quot;return function #{name}(){
    var _this = this;
    if ( !(this instanceof #{name}) ) {
      _this = new #{name}(&#39;#{delay}&#39;);
      _this.__autonew__ = true;
    }

    if (arguments &amp;&amp; arguments[0] != &#39;#{delay}&#39;)
      #{name}.prototype.constructor.apply(_this, arguments);

    return _this;
  }
  &quot;&quot;&quot;
  )();

  # Default the constructor to call its base
  if args.base? and ((not args.constructor?) or (not args.hasOwnProperty(&#39;constructor&#39;)))
    args.constructor = -&gt;
      Out.base?.constructor.apply @, arguments

  # Inherit if necessary
  if args.base?
    _.inherit Out, args.base

  # Add the constructor as a method
  oj.addMethod Out::, &#39;constructor&#39;, args.constructor

  # Mark new type and its instances with a non-enumerable type and isOJ properties
  typeProps =
    type: {value:Out, writable:false, enumerable:false}
    typeName: {value:name, writable:false, enumerable:false}
    isOJ: {value:true, writable:false, enumerable:false}
  oj.addProperties Out, typeProps
  oj.addProperties Out::, typeProps

  # Add properties helper to instance
  propKeys = (_.keys args.properties).sort()
  if Out::properties?
    propKeys = _.uniqueSortedUnion Out::properties, propKeys
  properties = value:propKeys, writable:false, enumerable:false
  # propKeys.has = _.reduce propKeys, ((o,item) -&gt; o[item.key] = true; o), {}
  oj.addProperty Out::, &#39;properties&#39;, properties

  # Add methods helper to instance
  methodKeys = (_.keys args.methods).sort()
  if Out::methods?
    methodKeys = _.uniqueSortedUnion Out::methods, methodKeys
  methods = value:methodKeys, writable:false, enumerable:false
  # methodKeys.has = _.reduce methodKeys, ((o,item) -&gt; o[item.key] = true; o), {}
  oj.addProperty Out::, &#39;methods&#39;, methods

  # Add methods to the type
  _.extend args.methods,

    # get: Get all properties, or get a single property
    get: (k) -&gt;
      if oj.isString k
        if @has k
          return @[k]
        else
          return undefined
      else
        out = {}
        for p in @properties
          out[p] = @[p]
        out

    # set: Set all properties on the object at once
    set: (k,v) -&gt;
      obj = k
      # Optionally take key, value instead of object
      if not oj.isObject k
        obj = {}
        obj[k] = v;

      # Set all keys that are valid properties
      for key,value of obj
        if @has key
          @[key] = value
      return

    # has: Determine if property exists
    # TODO: Make this O(1)
    has: (k) -&gt;
      _.some @properties, (v) -&gt; v == k

    # can: Determine if method exists
    # TODO: Make this O(1)
    can: (k) -&gt;
      _.some @methods, (v) -&gt; v == k

    # toJSON: Use properties to generate json
    toJSON: -&gt;
      json = {}
      for prop in @properties
        json[prop] = @[prop]
      json

  # Add methods
  oj.addMethods Out::, args.methods

  # Add the properties
  oj.addProperties Out::, args.properties

  Out

# argumentsUnion:
# Take arguments and tranform them into options and args.
# options is a union of all items in `arguments` that are objects
# args is a concat of all arguments that aren&#39;t objects in the same order
oj.argumentsUnion = (argList) -&gt;
  obj = {}
  list = []
  for v in argList
    if oj.isObject v
      obj = _.extend obj, v
    else
      list.push v
  options: obj, args: list

# oj.enum
# ------------------------------------------------------------------------
oj.enum = (name, args) -&gt;
  throw &#39;NYI&#39;

# oj.View
# ------------------------------------------------------------------------------

oj.View = oj.type &#39;View&#39;,

  # Views are special objects map properties together. This is a union of arguments
  # With the remaining arguments becoming a list

  constructor: (options = {}) -&gt;
    # console.log &quot;View.constructor: &quot;, JSON.stringify arguments

    throw new Error(&quot;oj.#{@typeName}: constructor failed to set this.el&quot;) unless oj.isDOM @el

    # Set instance on @el
    _setInstanceOnElement @el, @

    # Emit as a tag if new wasn&#39;t called
    @emit() if @.__autonew__

    # Generate id if missing
    options.id ?= oj.id()

    # Add class oj-typeName
    @$el.addClass &quot;oj-#{@typeName}&quot;

    # Views automatically set all options to their properties
    # arguments directly to properties
    @set options

    # Remove options that were set
    options = _.omit options, @properties...

    # Views pass through remaining options to be attributes on the root element
    # This can include jquery events and interpreted arguments
    @addAttributes options

    # Record if view is fully constructed
    @_isConstructed = true

  properties:

    # The element backing the View
    el:
      get: -&gt; @_el
      set: (v) -&gt;
        # Set the element directly if this is a dom element
        if oj.isDOMElement v
          @_el = v
          # Clear cache of $el
          @_$el = null
        # Generate the element from ojml
        else
          {dom:@_el, cssMap:@cssMap} = oj.compile css:0, cssMap:1, dom:1, html:0, v
        return

    # Get and cache jquery-enabled element (readonly)
    $el: get: -&gt; @_$el ? (@_$el = $ @el)

    # Get all attributes (readonly)
    attributes: get: -&gt;
      out = {}
      $.each @el.attributes, (index, attr) -&gt; out[ attr.name ] = attr.value;
      out

    # Get and set id of view from attribute
    id:
      get: -&gt; @$el.attr &#39;id&#39;
      set: (v) -&gt; @$el.attr &#39;id&#39;, v

    # CSS generated for this instance
    cssMap:
      get: -&gt; @_cssMap ? {}
      set: (v) -&gt;
        @_cssMap = v

    # Determine if this view has been fully constructed (readonly)
    isConstructed: get: -&gt; @_isConstructed ? false

    # Determine if this view has been fully inserted (readonly)
    isInserted:
      get: -&gt; @_isInserted ? false

  methods:


    # Mirror backbone view&#39;s find by selector
    $: -&gt; @$el.find arguments...

    # Add a single attribute
    addAttribute: (name,value) -&gt;
      attr = {}
      attr[name] = value
      @addAttributes attr

    # Add attributes and apply the oj magic with jquery binding
    addAttributes: (attributes) -&gt;
      attr = _.clone attributes

      events = _attributesProcessedForOJ attr

      # Add attributes as object
      if oj.isObject attr
        for k,v of attr

          # Wrap k in quotes if it has whitespace
          if k == &#39;class&#39;
            @$el.addClass v

          # Boolean attributes have no value
          else if v == true
            att = document.createAttribute k
            @el.setAttributeNode att

          # Otherwise add it normally
          else
            @$el.attr k, v

      # Bind events
      if events?
        _attributesBindEventsToDOM events, @el
      return

    # Remove a single attribute
    removeAttribute: (name) -&gt;
      @$el.removeAttr name
      return

    # Remove multiple attributes
    removeAttributes: (list) -&gt;
      for k in list
        @removeAttribute k
      return

    # emit: Emit instance as a tag function would do
    emit: -&gt; _.argumentsAppend @; return

    # Convert View to html
    toHTML: (options) -&gt;
      @el.outerHTML + (if options?.debug then &#39;\n&#39; else &#39;&#39;)

    # Convert View to dom (for compiling)
    toDOM: -&gt; @el

    # Convert
    toCSS: (debug) -&gt; _cssFromObject @cssMap, debug

    # Convert
    toCSSMap: -&gt; @cssMap

    # Convert View to string (for debugging)
    toString: -&gt; @toHTML()

    # # Detach element from dom
    # detach: -&gt; throw &#39;detach nyi&#39;
    #   # The implementation is to set el manipulate it, and remember how to set it back

    # # Attach element to dom where it use to be
    # attach: -&gt; throw &#39;attach nyi&#39;
    #   # The implementation is to unset el from detach

    # inserted is called the instance is inserted in the dom (override)
    inserted: -&gt;
      @_isInserted = true

# oj.CollectionView
# ------------------------------------------------------------------------------
oj.CollectionView = oj.type &#39;CollectionView&#39;,
  base: oj.View

  constructor: (options) -&gt;
    # console.log &quot;CollectionView constructor: &quot;, arguments

    @each = oj.argumentShift options, &#39;each&#39; if options?.each?
    @models = oj.argumentShift options, &#39;models&#39; if options?.models?

    oj.CollectionView.base.constructor.apply @, arguments

    # Once everything is constructed call make precisely once.
    @make()

  properties:

    each:
      get: -&gt; @_each
      set: (v) -&gt;
        @_each = v;
        @make() if @isConstructed
        return

    models:
      get: -&gt; @_models
      set: (v) -&gt;
        # Unbind events if collection
        if oj.isFunction @_models?.off
          @_models.off &#39;add remove change reset destroy&#39;, null, @

        @_models = v

        # Bind events if collection
        if oj.isFunction @_models?.on
          @_models.on &#39;add&#39;, @collectionAdded, @
          @_models.on &#39;remove&#39;, @collectionRemoved, @
          @_models.on &#39;change&#39;, @collectionChanged, @
          @_models.on &#39;reset&#39;, @collectionReset, @
          @_models.on &#39;destroy&#39;, @collectionDestroyed, @

        @make() if @isConstructed

        return

  methods:
    # Override make to create your view
    make: -&gt; throw &quot;oj.#{typeName}: make not implemented&quot;

    # Override these events to minimally update on change
    collectionAdded: (model, collection) -&gt; @make()
    collectionRemoved: (model, collection, options) -&gt; @make()
    collectionReset: (collection, options) -&gt; @make()
    collectionChanged: (model, collection, options) -&gt; # Do nothing
    collectionDestroyed: (collection, options) -&gt; @make()

# oj.ModelView
# ------------------------------------------------------------------------------
# Model view base class
oj.ModelView = oj.type &#39;ModelView&#39;,
  base: oj.View

  constructor: (options) -&gt;

    @value = oj.argumentShift options, &#39;value&#39; if options?.value?
    @model = oj.argumentShift options, &#39;model&#39; if options?.model?

    oj.ModelView.base.constructor.apply @, arguments

  properties:
    model:
      get: -&gt; @_model
      set: (v) -&gt;
        # Unbind events on the old model
        if oj.isEvented @_model
          @_model.off &#39;change&#39;, null, @

        @_model = v;

        # Bind events on the new model
        if oj.isEvented @_model
          @_model.on &#39;change&#39;, @modelChanged, @

        # Trigger change manually when settings new model
        @modelChanged()
        return

  methods:

    # Override modelChanged if you don&#39;t want a full remake
    modelChanged: -&gt;
      @$el.oj =&gt;
        @make @mode

    make: (model) -&gt; throw &quot;oj.#{@typeName}: make not implemented&quot;

# oj.ModelKeyView
# ------------------------------------------------------------------------------
# Model key view base class
oj.ModelKeyView = oj.type &#39;ModelKeyView&#39;,
  # Inherit ModelView to handle model and bindings
  base: oj.ModelView

  constructor: (options) -&gt;
    # console.log &quot;ModelKeyView.constructor: &quot;, JSON.stringify arguments
    @key = oj.argumentShift options, &#39;key&#39; if options?.key?

    # Call super to bind model and value
    oj.ModelKeyView.base.constructor.apply @, arguments

  properties:
    # Key used to access model
    key: null

    # Value directly gets and sets to the dom
    # when it changes it must trigger viewChanged
    value:
      get: -&gt; throw &quot;#{@typeName} value getter not implemented&quot;
      set: (v) -&gt; throw &quot;#{@typeName} value setter not implemented&quot;

  methods:
    # When the model changes update the value
    modelChanged: -&gt;
      if @model? and @key?
        # Update the view if necessary
        if not @_viewUpdatedModel
          @value = @model.get @key
      return

    # When the view changes update the model
    viewChanged: -&gt;
      # Delay view changes because many of them hook before controls update
      setTimeout (=&gt;
        if @model? and @key?
          # Ensure view changes aren&#39;t triggered twice
          @_viewUpdatedModel = true
          @model.set @key, @value
          @_viewUpdatedModel = false
        return
        ), 10
      return

# oj.TextBox
# ------------------------------------------------------------------------------
# TextBox control

oj.TextBox = oj.type &#39;TextBox&#39;,

  base: oj.ModelKeyView

  constructor: -&gt;
    {options, args} = oj.argumentsUnion arguments

    @el = oj =&gt;
      oj.input type:&#39;text&#39;,
        keydown: =&gt; if @live then @viewChanged(); return
        keyup: =&gt; if @live then @viewChanged(); return
        change: =&gt; @viewChanged(); return

    # Value can be set by argument
    @value = args[0] if args.length &gt; 0

    # Set live if it exists
    @live = oj.argumentShift options, &#39;live&#39; if options?.live?

    oj.TextBox.base.constructor.apply @, [options]

  properties:
    value:
      get: -&gt;
        v = @el.value
        v = &#39;&#39; if not v? or v == &#39;undefined&#39;
        v
      set: (v) -&gt;
        @el.value = v; return

    # Live update model as text changes
    live: true

# oj.CheckBox
# ------------------------------------------------------------------------------
# CheckBox control

oj.CheckBox = oj.type &#39;CheckBox&#39;,
  base: oj.ModelKeyView

  constructor: -&gt;
    {options, args} = oj.argumentsUnion arguments

    @el = oj =&gt;
      oj.input type:&#39;checkbox&#39;,
        change: =&gt; @viewChanged(); return

    # Value can be set by argument
    @value = args[0] if args.length &gt; 0

    oj.CheckBox.base.constructor.call @, options

  properties:
    value:
      get: -&gt; @el.checked
      set: (v) -&gt;
        v = !!v
        @el.checked = v
        if v
          @$el.attr &#39;checked&#39;, &#39;checked&#39;
        else
          @$el.removeAttr &#39;checked&#39;
        return

# oj.TextArea
# ------------------------------------------------------------------------------
# TextArea control

oj.TextArea = oj.type &#39;TextArea&#39;,
  base: oj.ModelKeyView

  constructor: -&gt;
    {options, args} = oj.argumentsUnion arguments

    @el = oj =&gt;
      oj.textarea
        keydown: =&gt; if @live then @viewChanged(); return
        keyup: =&gt; if @live then @viewChanged(); return
        change: =&gt; @viewChanged(); return

    # Value can be set by argument
    @value = args[0] if args.length &gt; 0

    oj.TextArea.base.constructor.call @, options

  properties:
    value:
      get: -&gt; @el.value
      set: (v) -&gt; @el.value = v; return

    # Live update model as text changes
    live: true

# oj.ListBox
# ------------------------------------------------------------------------------
# ListBox control

oj.ListBox = oj.type &#39;ListBox&#39;,
  base: oj.ModelKeyView

  constructor: -&gt;
    {options, args} = oj.argumentsUnion arguments

    @el = oj =&gt;
      oj.select change: =&gt; @viewChanged(); return

    # @options is a list of elements
    @options = oj.argumentShift options, &#39;options&#39;

    # Value can be set by argument
    @value = args[0] if args.length &gt; 0

    oj.ListBox.base.constructor.apply @, [options]

  properties:
    value:
      get: -&gt; @$el.val()
      set: (v) -&gt; @$el.val(v); return

    options:
      get: -&gt; @_options
      set: (v) -&gt;
        throw new Error(&#39;oj.ListBox::options array is missing&#39;) unless oj.isArray v
        @_options = v
        @$el.oj -&gt;
          for op in v
            oj.option op
          return
        return

# oj.Button
# ------------------------------------------------------------------------------
# Button control

oj.Button = oj.type &#39;Button&#39;,
  base: oj.View

  constructor: (args) -&gt;
    {options, args} = oj.argumentsUnion arguments

    # Label is first argument
    options.label ?= if args.length &gt; 0 then args[0] else &#39;&#39;

    @el = oj =&gt;
      oj.button options.label

    oj.Button.base.constructor.apply @, [options]

# oj.Link
# ------------------------------------------------------------------------------
# oj.Link = class Link inherits Control

# oj.List
# ------------------------------------------------------------------------------

# boundOrThrow: Determine if the index is in range after negative correction
# When out of bounds an error message is thrown

boundOrThrow = (ix, count, message) -&gt;
  # Correct negative indexes to be in range
  ixNew = if ix &lt; 0 then ix + count else ix
  unless 0 &lt;= ixNew and ixNew &lt; count
    throw new Error(message + &quot; is out of bounds (#{ix} in [0,#{count-1}])&quot;)
  ixNew

oj.List = oj.type &#39;List&#39;,
  base: oj.CollectionView

  constructor: -&gt;
    # console.log &quot;List constructor: &quot;, arguments
    {options, args} = oj.argumentsUnion arguments

    # tagName is write-once
    @_tagName = oj.argumentShift options, &#39;tagName&#39;

    @itemTagName = oj.argumentShift options, &#39;itemTagName&#39;

    # Generate el
    @el = oj =&gt;
      oj[@tagName]()

    # Use el if it was passed in
    @el = oj.argumentShift(options, &#39;el&#39;) if options.el?

    # Default @each function to pass through values
    options.each ?= (model) -&gt;
      if (oj.isString model) or (oj.isNumber model) or (oj.isBoolean model)
        model
      else
        JSON.stringify model

    # Args have been handled so don&#39;t pass them on
    oj.List.base.constructor.apply @, [options]

    # Set @items to options or args if they exist
    items = if args.length &gt; 0 then args else null
    @items = if options.items? then (oj.argumentShift options, &#39;items&#39;) else items

  properties:

    # items: get or set all items at once (readwrite)
    items:
      # Used cached items or items as interpreted by ojValue jquery plugin
      get: -&gt;
        return @_items if @_items?
        v = @$itemsEl.ojValue()
        if oj.isArray v then v else [v]

      set: (v) -&gt; @_items = v; @make(); return

    count: get: -&gt; @$itemsEl.length

    # tagName: name of root tag
    tagName: get: -&gt; @_tagName ? &#39;div&#39;

    # itemTagName: name of item tags
    itemTagName:
      get: -&gt; @_itemTagName ? &#39;div&#39;
      set: (v) -&gt; @_itemTagName = v; @make(); return

    # itemsEl: list of elements
    itemsEl: get: -&gt; @$itemsEl.get()

    # $itemsEl: list of jquery elements
    $itemsEl: get: -&gt; @_$itemsEl ? (@_$itemsEl = @$(&quot;&gt; #{@itemTagName}&quot;))

  methods:
    # Get item value at index
    item: (ix, ojml) -&gt;
      ix = boundOrThrow ix, @count, &quot;oj.List.item: index&quot;
      if ojml?
        @$itemEl(ix).oj ojml
        return
      else
        @$itemEl(ix).ojValue()

    # Get item element at index
    itemEl: (ix) -&gt;
      ix = boundOrThrow ix, @count, &quot;oj.List.itemEl: index&quot;
      @$itemsEl[ix]

    # Get item jquery element at index
    $itemEl: (ix) -&gt;
      ix = boundOrThrow ix, @count, &quot;oj.List.$itemEl: index&quot;
      @$itemsEl.eq(ix)

    # Remake everything
    make: -&gt;
      # Some properties call make before construction completes
      return unless @isConstructed

      # Convert models to views
      views = []
      if @models? and @each?
        models = if oj.isEvented @_models then @_models.models else @_models
        for model in models
          views.push @_itemFromModel model

      # Items are already views
      else if @items?
        views = @items

      # Render the views
      @$el.oj =&gt;
        for view in views
          @_itemElFromItem view

      @itemsChanged()
      return

    # Helper to map model to item
    _itemFromModel: (model) -&gt;
      oj =&gt;
        @each model

    # Helper to create itemTagName wrapped item
    _itemElFromItem: (item) -&gt;
      oj[@itemTagName] item

    add: (ix, ojml) -&gt;

      # Default to adding at the end
      if arguments.length == 1
        ojml = ix
        ix = @count

      ix = boundOrThrow ix, @count+1, &quot;oj.List.add: index&quot;

      tag = @itemTagName
      # Empty
      if @count == 0
        @$el.oj -&gt; oj[tag] ojml
      # Last
      else if ix == @count
        @$itemEl(ix-1).ojAfter -&gt; oj[tag] ojml
      # Not last
      else
        @$itemEl(ix).ojBefore -&gt; oj[tag] ojml

      @itemsChanged()
      return

    remove: (ix = -1) -&gt;
      ix = boundOrThrow ix, @count, &quot;oj.List.remove: index&quot;
      out = @item ix
      @$itemEl(ix).remove()
      @itemsChanged()
      out

    move: (ixFrom, ixTo = -1) -&gt;
      return if ixFrom == ixTo

      ixFrom = boundOrThrow ixFrom, @count, &quot;oj.List.move: fromIndex&quot;
      ixTo = boundOrThrow ixTo, @count, &quot;oj.List.move: toIndex&quot;

      if ixTo &gt; ixFrom
        @$itemEl(ixFrom).insertAfter @$itemEl(ixTo)
      else
        @$itemEl(ixFrom).insertBefore @$itemEl(ixTo)

      @itemsChanged()
      return

    swap: (ix1, ix2) -&gt;
      return if ix1 == ix2

      ix1 = boundOrThrow ix1, @count, &quot;oj.List.swap: firstIndex&quot;
      ix2 = boundOrThrow ix2, @count, &quot;oj.List.swap: secondIndex&quot;

      if Math.abs(ix1-ix2) == 1
        @move ix1, ix2
      else
        ixMin = Math.min ix1, ix2
        ixMax = Math.max ix1, ix2
        @move ixMax, ixMin
        @move ixMin+1, ixMax
      @itemsChanged()
      return

    unshift: (v) -&gt; @add 0, v; return

    shift: -&gt; @remove 0

    push: (v) -&gt; @add(v); return

    pop: -&gt; @remove -1

    clear: -&gt; @$itemsEl.remove(); @itemsChanged(); return

    # When items change clear relevant cached values
    itemsChanged: -&gt; @_$itemsEl = null; return

    # # On add minimally create the missing model
    collectionAdded: (m, c) -&gt;
      ix = c.indexOf m
      item = @_itemFromModel m
      @add ix, item
      return

    # On add minimally create the missing model
    collectionRemoved: (m, c, o) -&gt;
      @remove o.index
      return

    collectionReset: -&gt;
      @make()
      return

# oj.NumberList
# ------------------------------------------------------------------------------

oj.NumberList = -&gt;
  oj.List.call @, {tagName:&#39;ol&#39;, itemTagName:&#39;li&#39;}, arguments...

# oj.BulletList
# ------------------------------------------------------------------------------

oj.BulletList = -&gt;
  oj.List.call @, {tagName:&#39;ul&#39;, itemTagName:&#39;li&#39;}, arguments...

# oj.Table
# ------------------------------------------------------------------------------
# oj.Table = oj.type &#39;Table&#39;,
#   base: oj.CollectionView

#   properties:
#     rows: (list) -&gt;
#     rowCount: -&gt;
#     cellCount: -&gt;

#   methods:
#     row: (r) -&gt;
#     cell: (r, c) -&gt;

# oj.Table.Row = oj.type &#39;Table.Row&#39;,
#   base: oj.ModelView
#   properties:
#     row:
#       get: -&gt;
#       set: (list) -&gt;
#   methods:
#     cell: -&gt;

# oj.sandbox
# ------------------------------------------------------------------------------
# The sandbox is a readonly version of oj that is exposed to the user
oj.sandbox = {}
for key in _.keys oj
  if key.length &gt; 0 and key[0] != &#39;_&#39;
    oj.addProperty oj.sandbox, key, value:oj[key], writable:false

# oj.use
# ------------------------------------------------------------------------------
# Include a plugin of oj

oj.use = (plugin, settings = {}) -&gt;
  throw new Error(&#39;oj.use: function expected for first argument&#39;) unless oj.isFunction plugin
  throw new Error(&#39;oj.use: object expected for second argument&#39;) unless oj.isObject settings

  # Call plugin to gather extension map
  pluginMap = plugin oj, settings

  # Extend all properties
  for name,value of pluginMap
    oj[name] = value
    # Add to sandbox
    oj.addProperty oj.sandbox, name, value:value, writable: false


# jqueryExtend(fn)
# -----------------------------------------------------------------------------
#
#     $.fn.myExtension = jqueryExtend (($el,args) -&gt;
#       $el     # =&gt; The jquery matched element
#       args    # =&gt; Array of arguments
#       return a non-null value to stop iteration and return value to caller
#      ), (($el) -&gt;
#      ), isMap
jqueryExtend = (options = {}) -&gt;
  _.defaults options, get:_.identity, set:_.identity
  -&gt;
    args = _.toArray arguments
    $els = jQuery(@)
    # Map over jquery selection if no arguments
    if (oj.isFunction options.get) and args.length == 0
      out = []
      for el in $els
        out.push options.get $(el)
      # Unwrap arrays of length one
      if out.length == 1
        return out[0]
      out

    else if (oj.isFunction options.set)
      # By default return this for chaining
      out = $els
      for el in $els
        r = options.set $(el), args
        # Short circuit if anything is returned
        return r if r?

      $els

# jquery.oj
# -----------------------------------------------------------------------------
jQuery.fn.oj = jqueryExtend
  set:($el, args) -&gt;

    # No arguments return the first instance
    if args.length == 0
      return $el[0].oj

    # Compile ojml
    {dom,types} = oj.compile {dom:1,html:0,css:0}, args...

    # Reset content and append to dom
    $el.html &#39;&#39;
    dom = [dom] unless oj.isArray dom
    for d in dom
      $el.append d

    # Call inserted event on all types
    for t in types
      t.inserted()

    return

  get: ($el) -&gt;
    $el[0].oj

# jquery.ojValue
# -----------------------------------------------------------------------------
# Get the value of the selected element&#39;s contents
jQuery.fn.ojValue = jqueryExtend
  set: null
  get: ($el, args) -&gt;

    el = $el[0]
    child = el.firstChild

    switch oj.typeOf child
      # Parse the text to turn it into bool, number, or string
      when &#39;dom-text&#39;
        text = child.nodeValue
      # Get elements as oj instances or elements
      when &#39;dom-element&#39;
        if (inst = _getInstanceOnElement child)?
          inst
        else
          child

# jquery.ojAfter, jquery.ojBefore, ...
# -----------------------------------------------------------------------------

plugins =
  ojAfter: &#39;after&#39;
  ojBefore: &#39;before&#39;
  ojAppend: &#39;append&#39;
  ojPrepend: &#39;prepend&#39;
  ojReplaceWith: &#39;replaceWith&#39;
  ojWrap: &#39;wrap&#39;
  ojWrapInner: &#39;wrapInner&#39;

for ojName,jqName of plugins
  do (ojName, jqName) -&gt;
    jQuery.fn[ojName] = jqueryExtend
      set: ($el, args) -&gt;

        # Compile ojml for each one to separate references
        {dom,types} = oj.compile {dom:1,html:0}, args...

        # Append to the dom
        $el[jqName] dom

        for t in types
          t.inserted()

        return

      get:null</code></pre>
